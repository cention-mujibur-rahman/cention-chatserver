#! /usr/bin/env ferite

uses 'libweb/httpserver', 'libweb/consoleserver';
uses 'lib/FastChatMCAM';
uses 'lib/ExternalChat';
uses 'sys', 'date';

/* Placeholder variables that you can assign to */
global {
	object oo;
	string ss;
	array aa;
	number nn;
}

function dumphash (array a) {
	a.map() using (k,v) {
		Console.println("$k => $v");
	};
	return '';
}

object commandline = new ConsoleServer();

commandline.prompt('chatserver> ');

commandline.command('x','exit','quit') using (args) {
	EventLoop.stop();
};

commandline.command('d','descriptors') using (args) {
	Console.println(EventLoop.descriptors.join("\n"));
};

commandline.command('h','help') using (args) {
	Console.println([
		'help (h)       : Print this help',
		'descriptor (d) : Print list of file descriptors in use',
		'exit (x)       : Exit cleanly',
		'clear          : Clears screen',
		'',
		'Typing a valid Ferite expression prints the value',
		'of the expression.'
	].join("\n"));
};

commandline.command('clear') using (args) {
	Console.print("\033[2J\033[1;1H");
};

commandline.command('unknown') using (args) {
	// Eval ferite expression by default
	monitor {
		Console.println(eval('return ' + args.join(' ') + ';'));
	}
	handle {
		Console.println('< SYNTAX ERROR >');
	}
};
