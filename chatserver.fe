#! /usr/bin/env ferite

uses 'libweb/httpserver', 'libweb/consoleserver', 'lib/FastChatMCAM', 'sys';

/* Placeholder globals for debugging */
global {
	object oo;
	string ss;
	array aa;
	number nn;
}

class DummyApplication {
	string realName;
	boolean topLevel;
}

function dumphash (array a) {
	a.map() using (k,v) {
		Console.println("$k => $v");
	};
	return '';
}

object commandline = new ConsoleServer();
object chatserver = new HttpServer(9090);

commandline.prompt('chatserver> ');
application = new DummyApplication();
application.realName = 'cention-suite';

commandline.command('x','exit','quit') using (args) {
	Sys.exit(0);
};
commandline.command('d','descriptors') using (args) {
	Console.println(EventLoop.descriptors.join("\n"));
};
commandline.command('h','help') using (args) {
	Console.println([
		'help (h)       : Print this help',
		'descriptor (d) : Print list of file descriptors in use',
		'exit (x)       : Exit cleanly',
		'clear          : Clears screen',
		'',
		'Typing a valid Ferite expression prints the value',
		'of the expression.'
	].join("\n"));
};
commandline.command('clear') using (args) {
	Console.print("\033[2J\033[1;1H");
};
commandline.command('unknown') using (args) {
	// Eval ferite expression by default
	monitor {
		Console.println(eval('return ' + args.join(' ') + ';'));
	}
	handle {
		Console.println('< SYNTAX ERROR >');
	}
};

chatserver.onRequest() using (req) {
	array commands = [ 'MCAM' , req.pathInfo() ];
	
	// Console.println("\033[7m " + req.timestamp() + " \033[0m");
	
	monitor {
		if (req.request.method == 'POST') {
			req.onPostData() using (req,content) {
				req.query = req.parseQueryString(content);
				req.respond(handleFastChatMCAMAction(req,commands,req.query));
			};
		}
		else {
			req.respond(handleFastChatMCAMAction(req,commands,req.query));
		}
	}
	handle {
		Console.println("\033[41;33m##########[ERROR]##########\033[0m");
		Console.print("\033[31m");
		Console.println(err.str);
		Console.println("\033[0m");
	}
};

EventLoop.listen();
